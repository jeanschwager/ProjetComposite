 ***** Projet Optimisation materiaux composites 
 *** Jean Schwager 
 *********************** 
   
   
 **reglages du projet 
 OPTI DIME 3 ELEM TRI3; 
   
 *______________________Caracetristiques materiaux 
 E01=181e9; 
 E02=10.3e9; 
 NU0=0.28; 
 G0= 7.17e9; 
 tcou=0.1e-3; 
 *______________________ Caracteristiques géometriques 
 c0=0.5; 
 c1=0.5*c0; 
 sw0=0.5; 
 L0=0.5; 
   
 *---- nombre d'élément par droite ---- 
 nb=50; 
   
   
 *_______________________ Direction d'orthotropie 
   
   
 *---- Selon la direction x ---- 
   
 *dir0 = 1. 0. 0.; 
   
 *---- Selon la demi-corde ---- 
 Hyp1 = ((L0*L0)+(c0*c0))**(1./2.); 
 div1 = L0/Hyp1; 
 ang1 = ACOS div1; 
 dir0 = ((COS ang1) (SIN ang1) 0.); 
   
   
 *_______________________ Construction du mailllage 
   
 pO = 0. 0. 0. ; 
 pA = L0 sw0 0.; 
 pB = L0 (sw0+c1) 0.; 
 pC= 0. c0 0.; 
 p2 = L0 0. 0.; 
   
 l1= DROI nb pO pA; 
 l2= DROI nb pA pB; 
 l3= DROI nb pB pC; 
 l4= DROI nb pC pO; 
   
 cont1= l1 et l2 et l3 et l4; 
 s0 = SURF 'PLAN' (cont1); 
 *TRAC s0; 
   
 *---- Nombre de couches ---- 
 Ncou=16; 
   
 *---- Epaisseur de la plaque ---- 
 h0=Ncou*tcou; 
   
 *---- Modèle ---- 
 mo0 = MODE s0 'MECANIQUE' 'ELASTIQUE' 'ORTHOTROPE' 'DKT'; 
   
 *mat0= MATE mo0 'YG1' E01 'YG2' E02 'NU12' NU0 'G12' G0 'DIRECTION' pA 'PARALLELE' 'EPAI' h0; 
   
   
 *generation du profil de pression 
 xs0 = COOR 1 s0 ; 
 ys0 = COOR 2 s0 ; 
 * coordonnee y du bord d'attaque le long du span (fonction de x) 
 ysmin = sw0 / l0 * xs0 ; 
 * longueur de corde le long du span (fonction de x) 
 cc0 = ((c1 - c0) / l0 * xs0) + (MANU CHPO s0 SCAL c0) ; 
 * champ constant egal a 1 
 one = MANU CHPO s0 scal 1. ; 
 *profil aerodynamique de pression 
 yref0 = (ys0 - ysmin) / cc0 ; 
 pr0 = cc0 / c0 * (yref0 * ((one - yref0) ** 4)) ; 
 frc0 = PRESS COQU mo0 pr0 NORM ; 
   
 *_________________________________ Matrices de rigidité Q 
   
 n12c = (NU0*NU0); 
 Q11 = E01/(1/n12c); 
 Q22 = E02/(1-n12c); 
 Q12 = (E02*NU0)/(1-n12c); 
 Q66 = G0; 
   
 *______________________ Invariants 
   
 T0CB = (Q11-Q12-Q12+Q66+Q66+Q66+Q66+Q22)/8.; 
 T1CB = (Q11+Q12+Q12+Q22)/8.; 
 R0CB = (Q11-Q12-Q12-Q66-Q66-Q66-Q66+Q22)/8.; 
 R1CB = (Q11-Q22)/8.; 
   
 *____________________________________ Stratifie Angle Ply 
   
 *Creation de la liste des R1 
 pas1 = R1CB/50; 
 R1ap = PROG 0 PAS pas1 R1CB; 
 *Liste vide a remplir avec les Rk 
 RKap = PROG; 
 *Liste vide a remplir avec les energies 
 enelisap = PROG; 
 *longeur de la liste et de la boucle 
 nb1 = DIME R1ap; 
   
 REPE Boucle1 nb1; 
 R1ex = EXTR R1ap &Boucle1; 
 temp = 2*(R1ex/R1CB)*(R1ex/R1CB); 
 RKap2 = R0CB*(temp-1); 
 RKap = RKap et RKap2; 
   
 Q11ap = T0CB+T1CB+T1CB+Rkap2+R1ex+R1ex+R1ex+R1ex; 
 Q22ap = T0CB+T1CB+T1CB+Rkap2-R1ex-R1ex-R1ex-R1ex; 
 Q12ap = T1CB+T1CB-T0CB-Rkap2; 
 Q66ap = T0CB-Rkap2; 
   
 Ex = (Q11ap*Q22ap-(Q12ap*Q12ap))/Q22ap; 
 Ey = (Q11ap*Q22ap-(Q12ap*Q12ap))/Q11ap; 
 Gxy = Q66ap; 
 nuxy = Q12ap/Q22ap; 
   
 *___________________ Materiaux, rigidite et CL 
   
 * ------ Materiau 
   
 ma0 = MATE mo0 'DIRE' dir0 PARALLELE 'YG1' Ex 'YG2' Ey 'NU12' nuxy 
 'G12' Gxy 'EPAI' h0; 
   
 * ------ Rigidite 
 rig0 = RIGI mo0 ma0; 
   
 * ----- Conditions limites 
 cl0 = BLOQ DEPL ROTA l4; 
   
 * ------ Assemblage 
 rigtot1 = rig0 ET cl0; 
   
 *------- Résolution 
 usol = RESO rigtot1 frc0; 
   
 *__________________ Energie 
   
 sig1 = SIGMA usol mo0 ma0; 
 eps1 = EPSI mo0 usol ma0; 
 enedap = ENER mo0 eps1 sig1; 
 eneap = INTG mo0 enedap ma0; 
 enelisap = enelisap ET eneap; 
 FIN Boucle1; 
   
 aply = EVOL 'BLEU' 'MANU' 'LEGE' 'Angle-Ply' 
 'Rk*' Rkap 'R1*' R1ap; 
 DESS aply; 
   
 *_______________________________________________ Stratifié Cross Ply 
   
 *Creation de la liste des R1 
 R1cp = PROG 0 PAS pas1 R1CB; 
 *Liste vide a remplir avec les Rk 
 Rkcp = PROG ; 
 nb1 = DIME R1cp; 
 eneliscp = PROG; 
   
   
 REPE Boucle2 nb1; 
 Rkcp2 = R0CB; 
 Rkcp = Rkcp et Rkcp2; 
 FIN Boucle2; 
   
   
 REPE Boucle3 nb1 ; 
   
 R1ex2 = EXTR R1cp &Boucle3; 
 Rkex2 = EXTR Rkcp &boucle3; 
   
 Q11cp = T0CB+T1CB+T1CB+Rkex2+R1ex2+R1ex2+R1ex2+R1ex2; 
 Q22cp = T0CB+T1CB+T1CB+Rkex2-R1ex2-R1ex2-R1ex2-R1ex2; 
 Q12cp = T1CB+T1CB-T0CB-Rkex2; 
 Q66cp = T0CB-Rkex2; 
   
 Ex = (Q11cp*Q22cp-(Q12cp*Q12cp))/Q22cp; 
 Ey = (Q11cp*Q22cp-(Q12cp*Q12cp))/Q11cp; 
 Gxy = Q66cp; 
 nuxy = Q12cp/Q22cp; 
   
 *___________________ Materiaux, rigidite et CL 
   
 * ---------- Matériau 
   
 ma0 = MATE mo0 'DIRE' dir0 PARALLELE 'YG1' Ex 'YG2' Ey 'NU12' nuxy 
 'G12' Gxy 'EPAI' h0; 
   
 * ---------- Rigidite 
   
 rig0 = RIGI mo0 ma0; 
   
 * ---------- Conditions limites 
   
 cl0 = BLOQ DEPL ROTA l4; 
   
 *----------- Assemblage 
   
 rigtot = rig0 ET cl0; 
   
 *----------- Resolution 
   
 usol = RESO rigtot frc0; 
   
 *__________________ Energie 
   
 sig2 = SIGMA usol mo0 ma0; 
 eps2 = EPSI mo0 usol ma0; 
 ene2 = ENER mo0 eps2 sig2; 
 enet = INTG mo0 ene2 ma0; 
 eneliscp = eneliscp ET enet; 
 FIN Boucle3; 
   
 * ----------- Droite du cross ply 
 cply = EVOL 'ROUG' 'MANU' 'LEGE' 'Cross-Ply' 
 'Rk*' Rkcp 'R1*' R1cp; 
 DESS cply; 
   
 *___________________________________Symetrie du carré 
   
 enelissc = PROG; 
 R1sc = PROG; 
 NR0CB = -1*R0CB; 
 Rksc = PROG NR0CB PAS pas1 R0CB  ; 
 nb1 = DIME Rksc; 
   
 REPE Boucle4 nb1 ; 
 R1sc2 = 0.; 
 R1sc = R1sc et R1sc2; 
 FIN Boucle4; 
   
 *------------- Droite de la symetrie du carre 
 symc = EVOL 'VERT' 'MANU' 'LEGE' 'Sym-carree' 
 'Rk*' Rksc 'R1*' R1sc; 
 DESS symc; 
   
 REPE Boucle5 nb1 ; 
 R1ex = EXTR R1sc &Boucle5; 
 Rke2 = EXTR Rksc &Boucle5; 
   
 Q11sc = T0CB+T1CB+T1CB+Rke2+R1ex+R1ex+R1ex+R1ex; 
 Q22sc = T0CB+T1CB+T1CB+Rke2-R1ex-R1ex-R1ex-R1ex; 
 Q12sc = T1CB+T1CB-T0CB-Rke2; 
 Q66sc = T0CB-Rke2; 
   
 Ex = (Q11sc*Q22sc-(Q12sc*Q12sc))/Q22sc; 
 Ey = (Q11sc*Q22sc-(Q12sc*Q12sc))/Q11sc; 
 Gxy = Q66sc; 
 nuxy = Q12sc/Q22sc; 
   
 *___________________ Materiaux, rigidite et CL 
   
 ma0 = MATE mo0 'DIRE' dir0 PARALLELE 'YG1' Ex 'YG2' Ey 'NU12' nuxy 
 'G12' Gxy 'EPAI' h0; 
 rig0 = RIGI mo0 ma0; 
   
 cl0 = BLOQ DEPL ROTA l4; 
 rigtot = rig0 ET cl0; 
 usol = RESO rigtot frc0; 
   
 *__________________ Energie 
   
 sig3 = SIGMA usol mo0 ma0; 
 eps3 = EPSI mo0 usol ma0; 
 ene3 = ENER mo0 eps3 sig3; 
 enet = INTG mo0 ene3 ma0; 
 enelissc = enelissc ET enet; 
 FIN Boucle5; 
   
 *_______________________________________________ Minimisation de l energie 
   
 mintot = MINI (enelisap ET eneliscp ET enelissc); 
 MESS '------- l energie elastique minimale trouve par le programme est : ' mintot' -------------- '; 
 *Tracage du domaine des stratifies orthotropes 
 DESS (aply ET cply ET symc); 
   
   
 FIN; 
