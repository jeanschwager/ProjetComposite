**************************
* MARIE-SAINTE Marvin    *
* Commencé le 12/02/2018 *
* Projet de composites   *
**************************

***********
* Options * 
***********

OPTI 'DIME' 3 'ELEM' TRI3;
OPTI EPSI LINEAIRE;
*OPTI TRAC OPEN;

* Caractéristique du matériau 

E1 = 181.E9;
E2 = 10.3E9;
GG12 = 7.17E9;
nunu12 = 0.28;
ang = 0;
t0 = 0.1E-3;
N = 16;
H = N*t0;

* Géométrie de l'aile

L0 = 50.E-2;
c0 = 50.E-2;
sw0 = 50.E-2;
c1 = 0.5*c0;

* Direction d'orthotropie

*Selon la demi-corde;
Hyp1 = ((L0*L0)+(c0*c0))**(1./2.);
div1 = L0/Hyp1;
ang1 = ACOS div1;
*dir0 = ((COS ang1) (SIN ang1) 0.);

*Selon la couche de base;
h11 = c1+sw0-c0;
*h11 = h11-c0;
Hyp2 = ((L0*L0)+(h11*h11))**(1./2.);
div2 = L0/Hyp2;
ang2 = ACOS div2;
*dir0 = ((COS ang2) (SIN ang2) 0.);

*Selon la direction x;

dir0 = 1. 0. 0.;

*Selon la direction y;

*dir0 = 0. 1. 0;

* Points géométriques 

p0 = 0.0 0.0 0.0;
p1 = L0 sw0 0.0 ;
p2 = L0 (sw0+c1) 0.0 ;
p3 = 0 c0 0.0;

* TRAC (p0 ET p1 ET p2 ET p3);

* Droites géométriques

d0 = DROIT 50 p0 p1;
d1 = DROIT 50 p1 p2;
d2 = DROIT 50 p2 p3;
d3 = DROIT 50 p3 p0;

Wing = (d0 ET d1 ET d2 ET d3);

* Créaion du maillage

s0 = SURF Wing 'PLAN';
*TRAC s0;

**********
* Modèle * 
**********

mo0 = MODE s0 MECANIQUE ELASTIQUE ORTHOTROPE DKT;

**************
* Chargement *
**************

* Création du profil de pression

xs0 = coor 1 s0 ;
ys0 = coor 2 s0 ;

* Coordonnée y du bord d'attaque le long du span
* (fonction de x)

ysmin = sw0 / l0 * xs0 ;

* Longueur de corde le long du span (fonction de x)

cc0 = ((c1 - c0) / l0 * xs0) + (manu chpo s0 scal c0) ;

* Champ constant égal à 1

one = manu chpo s0 scal 1. ;

* Profil aérodynamique de pression

yref0 = (ys0 - ysmin) / cc0 ;
pr0 = cc0 / c0 * (yref0 * ((one - yref0) ** 4)) ;
frc0 = press coqu mo0 pr0 norm ;

**************************
* Matrices de rigidité Q * 
**************************

n12c = (nunu12*nunu12);
Q11 = E1/(1/n12c);
Q22 = E2/(1-n12c);
Q12 = (E2*nunu12)/(1-n12c);
Q66 = GG12;

******************************
* Invariants des contraintes *
******************************

T0CB = (Q11-Q12-Q12+Q66+Q66+Q66+Q66+Q22)/8.;
T1CB = (Q11+Q12+Q12+Q22)/8.;
R0CB = (Q11-Q12-Q12-Q66-Q66-Q66-Q66+Q22)/8.;
R1CB = (Q11-Q22)/8.;

*list R1CB;

*************
* ANGLE-PLY *
*************

pas1 = R1CB/50;
R1ap = PROG 0 PAS pas1 R1CB;
RKap = PROG;
ENET1 = PROG;
nb1 = DIME R1ap;

REPE Boucle1 nb1;
R1ex = EXTR R1ap &Boucle1;
tt = 2*(R1ex/R1CB)*(R1ex/R1CB);
RKap2 = R0CB*(tt-1);
RKap = RKap et RKap2;

Q11ap = T0CB+T1CB+T1CB+Rkap2+R1ex+R1ex+R1ex+R1ex;
Q22ap = T0CB+T1CB+T1CB+Rkap2-R1ex-R1ex-R1ex-R1ex;
Q12ap = T1CB+T1CB-T0CB-Rkap2;
Q66ap = T0CB-Rkap2;

Ex = (Q11ap*Q22ap-(Q12ap*Q12ap))/Q22ap;
Ey = (Q11ap*Q22ap-(Q12ap*Q12ap))/Q11ap;
Gxy = Q66ap;
nuxy = Q12ap/Q22ap;

*******************************************************
* Matériau, Matrice de Rigidité et conditions limites *
*******************************************************

* Matériau

ma0 = MATE mo0 'DIRE' dir0 PARALLELE 'YG1' Ex 'YG2' Ey 'NU12' nuxy 
'G12' Gxy 'EPAI' H;

* Rigidité 

rig0 = RIGI mo0 ma0; 

* Conditions limites

cl0 = BLOQ DEPL ROTA d3;

* Assemblage
rigtot1 = rig0 ET cl0;

*Résolution

usol = RESO rigtot1 frc0;

***********************
* Calcul de l'énergie *
***********************

sig1 = SIGMA usol mo0 ma0;
eps1 = EPSI mo0 usol ma0;
ene1 = ENER mo0 eps1 sig1;
enet = INTG mo0 ene1 ma0;
enet1 = enet1 ET enet;
FIN Boucle1;

aply = EVOL 'BLEU' 'MANU' 'LEGE' 'Angle-Ply' 
'Rk*' Rkap 'R1*' R1ap;
DESS aply;

*************
* CROSS-PLY *
*************

* Listes à remplir 

R1cp = PROG 0 PAS pas1 R1CB;
Rkcp = PROG ;
nb1 = DIME R1cp;
enet2 = PROG;

REPE Boucle2 nb1;
Rkcp2 = R0CB;
Rkcp = Rkcp et Rkcp2;
FIN Boucle2;


REPE Boucle3 nb1 ;
R1ex2 = EXTR R1cp &Boucle3;
Rkex2 = EXTR Rkcp &boucle3;

Q11cp = T0CB+T1CB+T1CB+Rkex2+R1ex2+R1ex2+R1ex2+R1ex2;
Q22cp = T0CB+T1CB+T1CB+Rkex2-R1ex2-R1ex2-R1ex2-R1ex2;
Q12cp = T1CB+T1CB-T0CB-Rkex2;
Q66cp = T0CB-Rkex2;

Ex = (Q11cp*Q22cp-(Q12cp*Q12cp))/Q22cp;
Ey = (Q11cp*Q22cp-(Q12cp*Q12cp))/Q11cp;
Gxy = Q66cp;
nuxy = Q12cp/Q22cp;

*******************************************************
* Matériau, Matrice de Rigidité et conditions limites *
*******************************************************

* Matériau 

ma0 = MATE mo0 'DIRE' dir0 PARALLELE 'YG1' Ex 'YG2' Ey 'NU12' nuxy 
'G12' Gxy 'EPAI' H;

* Rigidité 

rig0 = RIGI mo0 ma0; 

* Conditions limites 

cl0 = BLOQ DEPL ROTA d3;

*Assemblage

rigtot = rig0 ET cl0;

*Résolution

usol = RESO rigtot frc0;

***********************
* Calcul de l'énergie *
***********************

sig2 = SIGMA usol mo0 ma0;
eps2 = EPSI mo0 usol ma0;
ene2 = ENER mo0 eps2 sig2;
enet = INTG mo0 ene2 ma0;
enet2 = enet2 ET enet;
FIN Boucle3;

* On trace la droite des cross-ply 
cply = EVOL 'ROUG' 'MANU' 'LEGE' 'Cross-Ply' 
'Rk*' Rkcp 'R1*' R1cp;
DESS cply;

**********************
* SYMETRIE DU CARREE *
**********************

enet3 = PROG;
R1sc = PROG;
NR0CB = -1*R0CB;
Rksc = PROG NR0CB PAS pas1 R0CB  ;
nb1 = DIME Rksc;

REPE Boucle4 nb1 ;
R1sc2 = 0.;
R1sc = R1sc et R1sc2;
FIN Boucle4;

* On trace la droite de la symetrie carree
symc = EVOL 'VERT' 'MANU' 'LEGE' 'Sym-carree' 
'Rk*' Rksc 'R1*' R1sc;
DESS symc;

REPE Boucle5 nb1 ;
R1ex = EXTR R1sc &Boucle5;
Rke2 = EXTR Rksc &Boucle5;

Q11sc = T0CB+T1CB+T1CB+Rke2+R1ex+R1ex+R1ex+R1ex;
Q22sc = T0CB+T1CB+T1CB+Rke2-R1ex-R1ex-R1ex-R1ex;
Q12sc = T1CB+T1CB-T0CB-Rke2;
Q66sc = T0CB-Rke2;

Ex = (Q11sc*Q22sc-(Q12sc*Q12sc))/Q22sc;
Ey = (Q11sc*Q22sc-(Q12sc*Q12sc))/Q11sc;
Gxy = Q66sc;
nuxy = Q12sc/Q22sc;

*******************************************************
* Matériau, Matrice de Rigidité et conditions limites *
*******************************************************

ma0 = MATE mo0 'DIRE' dir0 PARALLELE 'YG1' Ex 'YG2' Ey 'NU12' nuxy 
'G12' Gxy 'EPAI' H;
rig0 = RIGI mo0 ma0; 

cl0 = BLOQ DEPL ROTA d3;
rigtot = rig0 ET cl0;
usol = RESO rigtot frc0;

***********************
* Calcul de l'énergie *
***********************
sig3 = SIGMA usol mo0 ma0;
eps3 = EPSI mo0 usol ma0;
ene3 = ENER mo0 eps3 sig3;
enet = INTG mo0 ene1 ma0;
enet3 = enet3 ET enet;
FIN Boucle5;

*****************************
* Minimisation de l'énergie *
*****************************

minen1 = MINI enet1;
minen2 = MINI enet2;
minen3 = MINI enet3;
list minen1;
list minen2;
list minen3;
mintot = MINI (enet1 ET enet2 ET enet3);
MESS 'L energie elastique minimale est : ' mintot;

*Tracage du domaine des stratifies orthotropes
DESS (aply ET cply ET symc);
